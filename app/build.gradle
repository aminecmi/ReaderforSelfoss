buildscript {
}

ext {
    configuration = [
            buildDate: new Date()
    ]
    // This will make me able to build multiple times a day. May break thinks. I may forget it.
    todaysBuilds = "1"
}

def gitVersion() {
    def process = "git for-each-ref refs/tags --sort=-authordate --format='%(refname:short)' --count=1".execute()
    return process.text.replaceAll("'", "").substring(1).replaceAll("\\.", "").trim()
}

def versionCodeFromGit() {
    println "version code " + gitVersion()
    return gitVersion().toInteger()
}

def versionNameFromGit() {
    println "version name " + gitVersion()
    return gitVersion()
}

apply plugin: 'kotlin-kapt'

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "apps.amine.bou.readerforselfoss"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode versionCodeFromGit()
        versionName versionNameFromGit()

        // Enabling multidex support.
        multiDexEnabled true
        lintOptions {
            abortOnError true
            disable 'InvalidPackage'
        }
        vectorDrawables.useSupportLibrary = true

        // tests
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
        debug {
            buildConfigField "String", "LOGIN_URL", appLoginUrl
            buildConfigField "String", "LOGIN_USERNAME", appLoginUsername
            buildConfigField "String", "LOGIN_PASSWORD", appLoginPassword
        }
    }
    flavorDimensions "build"
    productFlavors {
        githubConfig {
            versionNameSuffix '-github'
            dimension "build"
        }
        storeConfig {
            // As jenkins publishes to alpha first, this is the default suffix now.
            versionNameSuffix '-store'
            dimension "build"
        }
    }
}

dependencies {
    // Testing
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-beta02'
    androidTestImplementation 'androidx.test:runner:1.1.0-beta02'
    // Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks, CountingIdlingResource
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0-beta02'
    // Espresso-intents for validation and stubbing of Intents
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.0-beta02'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Android Support
    implementation "androidx.appcompat:appcompat:$android_version"
    implementation "com.google.android.material:material:$android_version"
    implementation "androidx.recyclerview:recyclerview:$android_version"
    implementation "androidx.legacy:legacy-support-v4:$android_version"
    implementation "androidx.vectordrawable:vectordrawable:$android_version"
    implementation "androidx.browser:browser:$android_version"
    implementation "androidx.cardview:cardview:$android_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'

    //multidex
    implementation 'androidx.multidex:multidex:2.0.0'

    // About
    implementation('com.mikepenz:aboutlibraries:6.2.0@aar') {
        transitive = true
    }

    // Retrofit + http logging + okhttp
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.burgstaller:okhttp-digest:1.12'

    // Material-ish things
    implementation 'com.ashokvarma.android:bottom-navigation-bar:2.0.5'
    implementation 'com.github.jd-alexander:LikeButton:0.2.3'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    // glide
    implementation 'com.github.bumptech.glide:glide:4.1.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.1.1'

    // Asking politely users to rate the app
    implementation 'com.github.stkent:amplify:2.2.0'

    // Drawer
    implementation 'co.zsmb:materialdrawer-kt:2.0.1'

    // Themes
    implementation 'com.52inc:scoops:1.0.0'
    implementation 'com.jaredrummler:colorpicker:1.0.2'
    implementation 'com.github.rubensousa:floatingtoolbar:1.5.1'

    // Pager
    implementation 'me.relex:circleindicator:2.0.0@aar'

    implementation 'androidx.core:core-ktx:1.0.0'

    // Crash
    implementation 'ch.acra:acra-http:5.2.1'
    implementation 'ch.acra:acra-dialog:5.2.1'

    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "android.arch.work:work-runtime-ktx:$work_version"
}


afterEvaluate {
    initAppLoginPropertiesIfNeeded()
}

def initAppLoginPropertiesIfNeeded() {
    def propertiesFile = file(System.getProperty("user.home") + '/.gradle/gradle.properties')
    if (!propertiesFile.exists()) {
        def commentMessage = "This is autogenerated local property from system environment to prevent key to be committed to source control."
        ant.propertyfile(file: System.getProperty("user.home") + "/.gradle/gradle.properties", comment: commentMessage) {
            entry(key: "appLoginUrl", value: System.getProperty("appLoginUrl"))
            entry(key: "appLoginUsername", value: System.getProperty("appLoginUsername"))
            entry(key: "appLoginPassword", value: System.getProperty("appLoginPassword"))
        }
    }
}